#!/usr/bin/perl

#
# Copyright 2020-2021 Luciano Xumerle. All rights reserved.
# Luciano Xumerle <luciano.xumerle@gmail.com>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;

my $file = '';
if ( defined $ARGV[0] && -f $ARGV[0] )
{
    $file = $ARGV[0];
}
else
{
    &help();
    exit;
}

my $isFlac = 0;
my @songs  = sort glob("*.mp3");
if ( $#songs < 0 )
{
    @songs = sort glob("*flac");
    $isFlac++;
}

my $tot = $#songs + 1;

open( TXT, $file ) || die;

my $artist = '';
my $album  = '';
my $year   = 0;
my @titles = ();

while (<TXT>)
{
    s/\s+$//;
    s/\s+##.*$//;

    if ( $artist eq '' && m/^\s*(.+)\s+::\s+(.+)\s*$/ )
    {
        $artist = $1;
        $album  = $2;
        if ( $album =~ m/^(.+)\s\((\d\d\d\d)\)$/ )
        {
            $album = $1;
            $year  = $2;
        }
    }

    if (m/^\s*(\d+)\.\s+(.+)\s*$/)
    {
        my $tartist = $artist;
        my $num     = $1;
        my $tit     = $2;

        if ( $tit =~ m/^\s*(.+)\s+::\s+(.+)\s*$/ )
        {
            $tartist = $1;
            $tit     = $2;
        }

        if ($isFlac)
        {
            push @titles,
              [
                'metaflac',           '--set-tag',
                qq|ARTIST=$tartist|,  '--set-tag',
                qq|ALBUM=$album|,     '--set-tag',
                qq|TRACKNUMBER=$num|, '--set-tag',
                qq|TITLE=$tit|,       '--set-tag',
                qq|DATE=$year|
              ];
        }
        else
        {
            push @titles,
              [
                'mp3info', '-a', $tartist, '-l', $album, '-n',
                $num,      '-t', $tit,     '-y', $year
              ];
        }
    }
}
close TXT;

if ( $#titles == $#songs )
{
    &help();

    if ($isFlac)
    {
        system( 'metaflac', '--remove-all', @songs );
    }
    else
    {
        system( 'id3v2', '-D', @songs );
    }

    for ( my $i = 0 ; $i <= $#songs ; $i++ )
    {
        system( @{ $titles[$i] }, $songs[$i] );
    }

    print STDERR qq|\n$tot files were tagged!\n\n|;

}
else
{
    &help();
}

#
# PRINT HELP
#
sub help
{
    print STDERR
qq|---------------------------------------------------------------------------
simpleMP3tag version 0.1.0 (Dec 31, 2021)
Copyright 2020 by Luciano Xumerle <luciano.xumerle\@gmail.com>
This is free software; see the source for copying conditions. There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
---------------------------------------------------------------------------\n\n|;

    print STDERR q|SYNTAX:

 - simpleMP3tag <file.txt>

The script takes the list of mp3 or flac files from the current
directory and loads TAG information from a formatted text file.

The format is:

ARTIST :: ALBUM TITLE (YEAR)

1. title1
2. title2
....

The Number of mp3 or flac files and tracks in txt file must be the same. 
The script uses mp3info and id3v2 to tag mp3s and metaflac to tag flacs.
|;
}
